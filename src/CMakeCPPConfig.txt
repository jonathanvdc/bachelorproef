#############################################################################
#  This file is part of the Stride software. 
#  It is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by 
#  the Free Software Foundation, either version 3 of the License, or any 
#  later version.
#  The software is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#  You should have received a copy of the GNU General Public License,
#  along with the software. If not, see <http://www.gnu.org/licenses/>.
#  see http://www.gnu.org/licenses/.
#
#  Copyright 2016, Willem L, Kuylen E & Broeckhove J
#############################################################################
#
#  This file contains the C++ compile & link configuration.
#  It is meant to be included in the src/CMakeLists.txt and 
#  to provide identical C++ configuration for the main/cpp 
#  and the test/ccp directories and subdirectories.
#
#############################################################################

#----------------------------------------------------------------------------
# Check CMAKE_BUILD_TYPE
#----------------------------------------------------------------------------
if( NOT (CMAKE_BUILD_TYPE MATCHES "Release" OR CMAKE_BUILD_TYPE MATCHES "Debug") )
	message(FATAL_ERROR  "========> CMAKE_BUILD_TYPE HAS TO MATCH EITHER Release OR Debug." )
endif()

#----------------------------------------------------------------------------
# Compile flags
#----------------------------------------------------------------------------
set( CMAKE_CXX_FLAGS         "${CMAKE_CXX_FLAGS} -Wall -Wno-unknown-pragmas -Wno-array-bounds -Wno-unused-private-field" )
set( CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -Ofast" )
set( CMAKE_CXX_FLAGS_DEBUG   "${CMAKE_CXX_FLAGS_DEBUG} -O0"   )

#----------------------------------------------------------------------------
# Platform dependent compile flags
#----------------------------------------------------------------------------
if( CMAKE_CXX_COMPILER_ID STREQUAL "Clang" AND CMAKE_HOST_APPLE )
	add_definitions( -D__APPLE__ )
	set( CMAKE_CXX_FLAGS           "${CMAKE_CXX_FLAGS} -std=c++14 -stdlib=libc++")
elseif( CMAKE_CXX_COMPILER_ID STREQUAL "Clang" AND NOT CMAKE_HOST_APPLE )
	set( CMAKE_CXX_FLAGS           "${CMAKE_CXX_FLAGS} -std=c++14" )
	add_definitions( -D__extern_always_inline=inline )
elseif( CMAKE_CXX_COMPILER_ID STREQUAL "GNU" )
	set( CMAKE_CXX_FLAGS 	       "${CMAKE_CXX_FLAGS} -fPIC -std=c++14" )
	set( CMAKE_C_FLAGS             "${CMAKE_C_FLAGS} -fPIC" )
endif()
#
include_directories( ${CMAKE_HOME_DIRECTORY}/main/cpp             )

#----------------------------------------------------------------------------
# Standard math lib
#----------------------------------------------------------------------------
set( LIBS   ${LIBS}   m )

#----------------------------------------------------------------------------
# TCLAP Library (command line processing)
#----------------------------------------------------------------------------
include_directories( SYSTEM ${CMAKE_HOME_DIRECTORY}/main/resources/lib/tclap/include )

#----------------------------------------------------------------------------
# Spdlog Library (logging)
#----------------------------------------------------------------------------
include_directories( SYSTEM ${CMAKE_HOME_DIRECTORY}/main/resources/lib/spdlog/include )

#----------------------------------------------------------------------------
# Tina's Random Number Generator (TRNG) library and paths
#----------------------------------------------------------------------------
include_directories( SYSTEM ${CMAKE_HOME_DIRECTORY}/main/resources/lib/trng-4.15/include )
#set( LIBS ${LIBS} trng )

#----------------------------------------------------------------------------
# Poco
#----------------------------------------------------------------------------
find_package( Poco COMPONENTS Net Util)
if( Poco_FOUND )
    include_directories( SYSTEM ${Poco_Net_INCLUDE_DIRS} )
    set( LIBS ${LIBS} ${Poco_LIBRARIES} )
endif()

#----------------------------------------------------------------------------
# MPI compile flags
#----------------------------------------------------------------------------
find_package( MPI )
include_directories( SYSTEM ${MPI_INCLUDE_PATH} )
set( LIBS ${LIBS} ${MPI_CXX_LIBRARIES} )

#----------------------------------------------------------------------------
# Boost
#----------------------------------------------------------------------------
set(BOOST_ROOT ${STRIDE_BOOST_ROOT})
find_package( Boost COMPONENTS filesystem thread date_time system mpi serialization REQUIRED )
include_directories( SYSTEM ${Boost_INCLUDE_DIRS} )
set( LIBS   ${LIBS} ${Boost_LIBRARIES} )

#----------------------------------------------------------------------------
# OpenMP
#----------------------------------------------------------------------------
if( NOT STRIDE_FORCE_NO_OPENMP )
	if ( NOT DEFINED HAVE_CHECKED_OpenMP )
	    set( HAVE_CHECKED_OpenMP  TRUE  CACHE  BOOL  "Have checked for OpenMP?" FORCE )
        find_package( OpenMP )
        if( OPENMP_FOUND )
            set( HAVE_FOUND_OpenMP  TRUE  CACHE  BOOL  "Have found OpenMP?" FORCE )
        else()
            set( HAVE_FOUND_OpenMP  FALSE  CACHE  BOOL  "Have found OpenMP?" FORCE )
	    endif()
    endif()
    if ( HAVE_FOUND_OpenMP )
    	set( OPENMP_FOUND TRUE )
        set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}" )
        set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}" )
    else()
    	# This is done to eliminate blank output of undefined CMake variables.
    	set( OPENMP_FOUND FALSE )
    endif()
else()
    # This is done to eliminate blank output of undefined CMake variables.
    set( OPENMP_FOUND FALSE )
    set( HAVE_CHECKED_OpenMP FALSE )
    set( HAVE_FOUND_OpenMP FALSE )
endif()
# If not found, use the dummy omp.
if( NOT OPENMP_FOUND )
    include_directories( ${CMAKE_HOME_DIRECTORY}/main/resources/lib/domp/include )
endif()

#----------------------------------------------------------------------------
# TBB
#----------------------------------------------------------------------------
find_package( TBB )
if( TBB_FOUND )
	include_directories( SYSTEM ${TBB_INCLUDE_DIR} )
	set( LIBS ${LIBS} ${TBB_LIBRARIES} )
	if ( NOT OPENMP_FOUND )
		# We didn't find OpenMP but we *did* find TBB. We should advise our parallelization
		# logic to use TBB then, which we'll do using a preprocessor directive.
		add_definitions(-DPARALLELIZATION_LIBRARY_TBB)
	endif()
else()
	if ( NOT OPENMP_FOUND )
		# We found neither TBB nor OpenMP. But we can still use STL threads for parallelization.
		add_definitions(-DPARALLELIZATION_LIBRARY_STL)
	endif()
endif()
 
#----------------------------------------------------------------------------
# HDF5 Library
# Try to find the C variant of libhdf5, if found, USE_HDF5 is defined
# and passed to the compilers to allow compilation of selective features
# through preprocessor commands like #ifdef USE_HDF5 and friends.
# Additional defs are required on Ubuntu where lib are installed 
# with hdf5 v1.6 as default behavior.
#----------------------------------------------------------------------------
if( STRIDE_FORCE_NO_HDF5 )
	message( STATUS "---> Skipping HDF5, STRIDE_FORCE_NO_HDF5 set.")
else()
	find_package( HDF5 COMPONENTS C HL )
	if( HDF5_FOUND )
		include_directories(SYSTEM ${HDF5_INCLUDE_DIRS} )
		set( LIBS   ${LIBS}   ${HDF5_LIBRARIES} )
		add_definitions( -DUSE_HDF5 -DH5_NO_DEPRECATED_SYMBOLS )
		add_definitions( -DH5Dcreate_vers=2 -DH5Dopen_vers=2 )
		add_definitions( -DH5Acreate_vers=2 -DH5Gcreate_vers=2 )
		add_definitions( -DH5Gopen_vers=2 )
	else()
		# This is done to eliminate blank output of undefined CMake variables.
		set( HDF5_FOUND FALSE )
	endif()    
endif()

#############################################################################
